// Names is a list of strings of the same length as Grades, while grades is a list of numbers.
// These numbers are associated with the names.  Initially, Names is sorted alphabetically.
// This procedure returns a pair of lists of the form
//         [SortedNames, SortedGrades]
// sucht that the pairs are sorted with respect to the numbers in grades.  The sublists of
// Names with the same grade are still sorted alphabetically.
countingSort := procedure(Names, Grades) {
    assert(#Names == #Grades, "$#Names$ != $#Grades$");
    maxGrade     := max(Grades);
    Counts       := [0] * maxGrade;
    Indices      := [];
    SortedNames  := [];
    SortedGrades := [];
    for (g in Grades) {
        Counts[g] += 1;
    }
    Indices[1] := 1;
    for (g in [2 .. maxGrade]) {
        Indices[g] := Indices[g-1] + Counts[g-1];
    }
    for (i in [1 .. #Names]) {
        g     := Grades[i];
        index := Indices[g];
        SortedNames [index] := Names[i];
        SortedGrades[index] := Grades[i];
        Indices[g]          += 1;
    }
    return [SortedNames, SortedGrades];
};
main := procedure() {
    Lines := readFile("counting-sort-data.txt");
    Names := [];
    Grades := [];
    for (i in [1 .. #Lines]) {
        [Names[i], Grades[i]] := split(Lines[i], ",");
        Grades[i] := int(Grades[i]);
    }
    Names := fillNames(Names);
    for (i in [1 .. #Lines]) {
        print("$Names[i]$: $Grades[i]$");
    }
    print("\nSorted List:");
    [SortedNames, SortedGrades] := countingSort(Names, Grades);
    for (i in [1 .. #Lines]) {
        print("$SortedNames[i]$: $SortedGrades[i]$");
    }
};
fillNames := procedure(Names) {
    maxLength := max([#s : s in Names]);
    return [s + " " * (maxLength + 1 - #s) : s in Names];
};

main();
